# enable this line for profiling (also zprof at the end of this file)
# zmodload zsh/zprof

# this function can be used in place of `source` to profile zsh startup time
function load() {
    timer=$(($(gdate +%s%N)/1000000))

    source $1

    now=$(($(gdate +%s%N)/1000000))
    elapsed=$(($now-$timer))
    echo $elapsed":" $1
}

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/projects

# initialize antibody plugin manager
source <(antibody init)
antibody bundle < ~/.zsh_plugins.txt

# initialize custom prompt
autoload -U promptinit
fpath=($HOME/.dotfiles/zsh-clean-prompt $fpath)
promptinit
prompt codesmith

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

os='unknown'
unamestr=`uname -s`
if [[ "$unamestr" == "Darwin" ]]
then
  os='OSX'
elif [[ "$unamestr" == "Linux" ]]
then
  os='LINUX'
fi
export OS=$os

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
setopt completealiases

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
unsetopt nomatch

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

#THIS MUST BE AT THE END OF THE FILE FOR SDKMAN TO WORK!!!
export SDKMAN_DIR="/Users/brian/.sdkman"
[[ -s "/Users/brian/.sdkman/bin/sdkman-init.sh" ]] && source "/Users/brian/.sdkman/bin/sdkman-init.sh"

# enable this line for profiling (also zmodload at the top of this file)
# zprof
